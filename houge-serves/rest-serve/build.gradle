apply plugin: "application"
apply plugin: "com.bmuschko.docker-remote-api"

mainClassName = "cool.houge.rest.main.RestMain"

jar {
  manifest {
    attributes "Multi-Release": "true"
  }
}

File dockerInputDir() {
  def f = file("${buildDir}/docker/")
  if (!f.exists()) {
    f.mkdirs()
  }
  return f
}

task copyDockerResource {
  group "docker"
  doLast {
    copy {
      from("${buildDir}/libs")
      into(dockerInputDir())
      include("*-all.jar")
      rename(".*-all.jar", "app.jar")
    }
    copy {
      from(projectDir)
      into(dockerInputDir())
      include("Dockerfile")
    }
  }
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  group "docker"
  dependsOn("shadowJar", "copyDockerResource")
  inputDir = dockerInputDir()
  images.addAll("kevin70/${project.name}:latest", "kevin70/${project.name}:${version}".toString())
}

dependencies {
  api("top.yein.chaos:chaos-core:1.0-SNAPSHOT")
  implementation(project(":houge-proto"))
  implementation("com.google.guava:guava")
  implementation("io.projectreactor.netty:reactor-netty")
  implementation("io.grpc:grpc-netty")
  implementation("com.salesforce.servicelibs:reactor-grpc-stub")

  implementation("com.fasterxml.jackson.core:jackson-databind")
  implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")
  implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
  implementation("io.projectreactor:reactor-tools")
}